export malloc;
export free;

export exit;
export assert;

export strlen;
export puts;
export print_num;

export fopen;
export fclose;
export fsize;
export fread;
export fwrite;

extern GetProcessHeap() -> void *;
extern HeapAlloc(heap: void *, flags: int, size: int) -> void *;
extern HeapFree (heap: void *, flags: int, ptr: void *);

extern GetStdHandle(std_handle: int) -> void *;
extern OpenFile(name : char *, ofstruct : void *, style : u32) -> void *;

extern GetFileSize(handle: void *, size_h: int *) -> int;

extern ReadFile (handle: void *, buffer: void *, bytes_to_read : int, bytes_read   : int *, overlapped: void *) -> bool;
extern WriteFile(handle: void *, buffer: void *, bytes_to_write: int, bytes_written: int *, overlapped: void *) -> bool;

extern CloseHandle(handle: void *);

extern ExitProcess(exit_code: int);

heap: void *;

func malloc(size: int) -> void * {
	if (heap == null) {
		heap = GetProcessHeap();
	}
	
	return HeapAlloc(heap, 0, size);
}

func free(ptr: void *) -> void {
	assert(ptr  != null);
	assert(heap != null);
	
	HeapFree(heap, 0, ptr);
}

func exit(status: int) -> void {
	ExitProcess(status);
}

func assert(expression: bool) -> void {
	if (!expression) {
		puts("*** ASSERTION FAILED ***");
		exit(-1);
	}
}

func strlen(str: char *) -> int {
	start := str;
	
	while (*str != '\0') str++;
	
	return str - start;
}

STD_OUTPUT_HANDLE: int = -11;

func puts(str: char *) -> void {
	std_handle: void * = GetStdHandle(STD_OUTPUT_HANDLE);

	len := strlen(str);

	bytes_written: int;
	WriteFile(std_handle, str, len, &bytes_written, null);
}

func print_num(num: int) -> void {
	num_str: char[32];
	idx: int = 0;

	 // Calculate required string length
	n := num;
	while (true) {
		n = n / 10;
		idx++;

		if (n == 0) break;
	}
	
	// Print number in reverse
	num_str[idx--] = '\0';
	
	while (true) {
		digit: char = cast(char) (num % 10);		
		num_str[idx--] = digit + '0';
		
		num = num / 10;
		if (num == 0) break;
	}
	
	puts(num_str);
}

OF_CREATE    : u32 = 0x00001000;
OF_READ      : u32 = 0x00000000;
OF_READWRITE : u32 = 0x00000002;

func fopen(name: char *, mode: char *) -> void * {
	struct OFSTRUCT {
		cBytes:     u8;
		fFixedDisk: u8;
		nErrCode:   u16;
		Reserved1:  u16;
		Reserved2:  u16;
		szPathName: char[128];
	}
	ofstruct: OFSTRUCT;
	
	flags : u32;
	
	if (mode[0] == 'r') {
		flags = OF_READ;
	} else if (mode[0] == 'w') {
		flags = OF_READWRITE | OF_CREATE;
	}
	
	return OpenFile(name, &ofstruct, flags);
}

func fclose(handle: void *) {
	CloseHandle(handle);
}

func fsize(handle: void *) -> int {
	return GetFileSize(handle, null);
}

func fread(handle: void *, bytes: char *, num_bytes: int) -> bool {
	num_bytes_read : int;
	return ReadFile(handle, bytes, num_bytes, &num_bytes_read, null);
}

func fwrite(handle: void *, bytes: char *, num_bytes: int) -> bool {
	num_bytes_written : int;
	return WriteFile(handle, bytes, num_bytes, &num_bytes_written, null);
}
